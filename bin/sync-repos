#!/usr/bin/env fish

function print_status_name
    echo (string replace --all _ " " "$argv")
end

function push_status
    set --append $argv[1] $argv[2]
    echo (set_color yellow)(print_status_name $argv[1])(set_color normal)\n
end

if test -n "$argv[1]"
    set root_dir "$argv[1]"
else
    set root_dir .
end

set no_git_dir
set no_remote_heads
set synced
set sync_failed

for repo_dir in $root_dir/*
    if not test -d $repo_dir
        continue
    end

    echo (set_color --bold magenta)$repo_dir(set_color normal)

    if not test -d $repo_dir/.git
        push_status no_git_dir $repo_dir
        continue
    end

    if not git -C $repo_dir ls-remote --heads --quiet --exit-code &>/dev/null
        push_status no_remote_heads $repo_dir
        continue
    end

    if git -C $repo_dir sync
        push_status synced $repo_dir
    else
        push_status sync_failed $repo_dir
    end
end

for one_summary in synced sync_failed no_git_dir no_remote_heads
    if test -n "$$one_summary"
        echo \n(set_color --bold blue)(print_status_name $one_summary):(set_color normal)
        printf "%s\n" $$one_summary
    end
end
